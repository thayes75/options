SUBROUTINE SFNTRI(DET,SF,GDSF,L1,L2,L3,ELXY)
  IMPLICIT NONE
! THIS IS THE SUBROUTINE THAT CALCULATES THE SHAPE FUNCTIONS 
! AND THEIR DERIVATIVES AT SPECIFIED GLOBAL POSITIONS
  INTEGER,PARAMETER :: NPE=3
  INTEGER :: I,J,K
  REAL,DIMENSION(3,2),INTENT(IN) :: ELXY
  REAL,INTENT(IN) :: L1,L2,L3
  REAL,INTENT(OUT) :: DET
  REAL,DIMENSION(3),INTENT(OUT) :: SF
  REAL,DIMENSION(2,3),INTENT(OUT) :: GDSF
  REAL,DIMENSION(3,3) :: DSF
  REAL,DIMENSION(2,2) :: GJ,GJINV
  REAL :: SUM

! INITIALIZE ARRAYS
  DO I=1,NPE  
     DSF(1,I) = 0.0
     DSF(2,I) = 0.0
     DSF(3,I) = 0.0
     SF(I)    = 0.0
  END DO
  
! SET THE SHAPE FUNCTIONS FOR A TRIANGLE
  SF(1) = L1
  SF(2) = L2
  SF(3) = L3
  DSF(1,1) = 1.0
  DSF(2,2) = 1.0
  DSF(3,3) = 1.0

! FIND THE JACOBIAN
  DO I=1,2
     DO J=1,2
        SUM = 0.0
        DO K=1,NPE
           SUM = SUM + (DSF(I,K) - DSF(3,K))*ELXY(K,J)
        END DO
        GJ(I,J) = SUM
     END DO
  END DO

! FIND GJ's INVERSE
  DET        =  GJ(1,1)*GJ(2,2) - GJ(1,2)*GJ(2,1)
  GJINV(1,1) =  GJ(2,2)/DET
  GJINV(2,2) =  GJ(1,1)/DET
  GJINV(1,2) = -1.0*GJ(1,2)/DET
  GJINV(2,1) = -1.0*GJ(2,1)/DET

! OBTAIN THE DERIVATIVE OF THE SHAPE FUNCTION WITH PROPER
! TRANSFORMATION
  DO I=1,2
     DO J=1,NPE
        SUM = 0.0
        DO K=1,2
           SUM = SUM + GJINV(I,K)*(DSF(K,J) - DSF(3,J))
        END DO
        GDSF(I,J) = SUM
     END DO
  END DO

END SUBROUTINE SFNTRI
